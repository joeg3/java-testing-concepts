plugins {
    id 'java-library'
    id 'checkstyle'
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.1' // required for parameterized tests
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
}

test {
    useJUnitPlatform {
        // You can run only tests tagged as 'it' for integration tests
        if (project.hasProperty('type') && project.getProperty('type') == 'it') {
            includeTags 'it'
            excludeTags 'unit', 'fuzzyit'
        }
    }

    dependsOn 'cleanTest' // Always run tests, even when nothing changed.

    testLogging {
        // Log result for each test as they run
        events "passed", "skipped", "failed", "standardOut", "standardError"

        exceptionFormat = 'full' // For test failures, show assertion on why it failed instead of just the line number of failure
        showStandardStreams = true // Show stdout and stderr of tests on console
    }

    // Show summary of test run
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

checkstyle { // Runs tasks checkstyleMain and checkstyleTest when source code changes
    toolVersion = '8.31'
    // You can specify the location of source and config files, but:
    // I think the source parameter for the checkstyleMain and checkstyleTest tasks defaults to src/main/java and src/test/java respectively
    // I think the config file defaults to <project_root>/config/checkstyle/checkstyle.xml
    // Report should be in <project_root>/build/reports/checkstyle/main.html
}