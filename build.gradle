plugins {
    id 'java-library'
}

repositories {
    jcenter()
}

dependencies {
    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.1' // required for parameterized tests

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
}

test {
    useJUnitPlatform {
        // You can run only tests tagged as 'it' for integration tests
        if (project.hasProperty('type') && project.getProperty('type') == 'it') {
            includeTags 'it'
            excludeTags 'unit', 'fuzzyit'
        }
    }

    dependsOn 'cleanTest' // Always run tests, even when nothing changed.

    testLogging {
        // Log result for each test as they run
        events "passed", "skipped", "failed", "standardOut", "standardError"

        // For test failures, instead of just showing the line number of failure on stdout,
        // also show assertion on why it failed
        exceptionFormat = 'full'

        showStandardStreams = true // Show stdout and stderr of tests on console
    }

    // Show summary of test run
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}
